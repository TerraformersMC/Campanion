plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
}

apply from: 'https://raw.githubusercontent.com/TerraformersMC/GradleScripts/2.1/ferry.gradle'

repositories {
	mavenLocal()
	maven {
		url "https://maven.jamieswhiteshirt.com/libs-release"
		content {
			includeGroup "com.jamieswhiteshirt"
		}
	}
	maven {
		name = "Ladysnake Libs"
		url = "https://ladysnake.jfrog.io/artifactory/mods"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:$project.minecraft_version"
	mappings "net.fabricmc:yarn:$project.yarn_mappings:v2"

	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")
	modImplementation("com.terraformersmc:dossier:${project.dossier_version}")
//	modNotTransitive("gravestones", "net.guavy:Gravestones:${project.gravestones_version}")
	modImplementation "dev.emi:trinkets:${trinkets_version}"

	include modImplementation("com.jamieswhiteshirt:reach-entity-attributes:${project.reach_entity_attributes_version}")
}

loom {
	accessWidenerPath =  file("src/main/resources/campanion.accesswidener")
}

subprojects.each {
	remapJar.dependsOn("${it.path}:remapJar")
}

def modNotTransitive(String localOverrideKey, Object dep) {
	File file = file("../.${localOverrideKey}-local");
	if (file.exists()) {
		dependencies.implementation(dep) {
			transitive = false
		}
	} else {
		dependencies.modImplementation(dep) {
			transitive = false
		}
	}
}

java {
	withSourcesJar()
}

repositories {
	maven {
		name = 'TerraformersMC'
		url = 'https://maven.terraformersmc.com/'
	}
}
